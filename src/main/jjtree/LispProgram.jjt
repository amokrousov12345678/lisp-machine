options {
  STATIC = false;
  MULTI = true;
  NODE_DEFAULT_VOID=true;
}

PARSER_BEGIN(LispStatement)
package ru.nsu.fit.amdp.lisp_machine.grammar;
public class LispStatement {}
PARSER_END(LispStatement)

SKIP : {
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN : {
   < Identifier: ["a"-"z","A"-"Z",".","*", "+", "!", "-", "_", "?", "<", ">", "=", "/", "&"] ( ["a"-"z","A"-"Z","0"-"9",".","*", "+", "!", "-", "_", "?", "<", ">", "=", "/", "&" ] )* >
  |
  < IntVal: ( ["0"-"9"] )+ >
  |
  < FloatVal: ( ["0"-"9"] )+ "." ( ["0"-"9"] )+  >
  |
  < StrVal: "\"" (~["\""])* "\"" >
}

void Identifier() #Identifier :
{Token t;}
{
  t=<Identifier>
  {
    jjtThis.jjtSetValue(t.image);
  }
}

void IntVal() #IntVal :
{Token t;}
{
  t=<IntVal>
  {
      jjtThis.jjtSetValue(Integer.valueOf(t.image));
  }
}

void FloatVal() #FloatVal :
{Token t;}
{
  t=<FloatVal>
  {
      jjtThis.jjtSetValue(Float.valueOf(t.image));
  }
}

void StrVal() #StrVal :
{Token t;}
{
  t=<StrVal>
  {
       jjtThis.jjtSetValue(t.image.substring(1,t.image.length()-1));
  }
}

void List() #List :
{}
{
  "(" (Identifier() | FloatVal() | IntVal() | StrVal() | List())* ")"
}

ASTLispExpressions LispExpressions() #LispExpressions :
{}
{
  (List())* <EOF>
   {return jjtThis;}
}